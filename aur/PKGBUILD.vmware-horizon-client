# Maintainer: Christian Hesse <mail@eworm.de>

pkgbase=vmware-horizon-client
pkgname=('vmware-horizon-client' 'vmware-horizon-pcoip' 'vmware-horizon-rtav' 'vmware-horizon-usb')
pkgver=3.4.0
_build=2769709
pkgrel=1
pkgdesc='VMware Horizon Client connect to VMware Horizon virtual desktop'
arch=('i686' 'x86_64')
url='https://my.vmware.com/web/vmware/info/slug/desktop_end_user_computing/vmware_horizon_clients/3_0'
license=('custom')
makedepends=('libxslt')
source=("https://download3.vmware.com/software/view/viewclients/CART15Q1/VMware-Horizon-Client-${pkgver}-${_build}.x86.bundle"
        'https://mirror.i3d.net/pub/gentoo-portage/eclass/vmware-bundle.eclass'
	'vmware-horizon-client.desktop'
	'vmware-horizon-usb'
	'vmware-horizon-usb.service'
        'vmware-horizon-usb-tmpfiles.conf')
sha256sums=('50901cae7a3c62b31b68e946a7b1e380f6c2c158aa8532dda536890a602e1d1b'
            'd8794c22229afdeb698dae5908b7b2b3880e075b19be38e0b296bb28f4555163'
            '21927f16cfb92ac07777297787106982b301b5d42ca068e052a01dc3f2cbb208'
            '7c78953823f7307814104881b322dcf66c36ca02e44e559239ac51abcf1e7a37'
            '5e737d69e49ea7e039bc94f358b45c8e6d9071b7c041a53800555d3dc21c8dac'
            'ec763930dd50d6e77a31c40c939909752cfb124cafb0a4ca4f76860375a14d75')

# We need these functions for the Gentoo eclass...
ebegin() {
	echo -n "Begin ${1}: "
}
eend() {
	echo 'done'
}

prepare() {
	# We need this variable for the Gentoo eclass...
	export T="${srcdir}"

	source "${srcdir}/vmware-bundle.eclass"

	for bundle in ${pkgname[@]}; do
	        vmware-bundle_extract-bundle-component "${srcdir}/VMware-Horizon-Client-${pkgver}-${_build}.x86.bundle" "${bundle}" "${srcdir}/extract/${bundle}"
	done

	# This is a dirty hack, but it works.
	# Change dynamic section in ELF files to fix dynamic linking.
	# Make sure the length is not changed!
	#	libudev.so.0 -> libudev.so.1
	#	libssl.so.1.0.1 -> libssl.so.1.0.0
	#	libcrypto.so.1.0.1 -> libcrypto.so.1.0.0
	for FILE in $(find "${srcdir}/extract/" -type f); do
		# executables and libraries only
		file --mime "${FILE}" | egrep -q "(application/x-(executable|sharedlib)|text/x-shellscript)" || continue

		# make executable
		chmod +x "${FILE}"

		# link against libudev.so.1
		sed -i -e 's/libudev.so.0/libudev.so.1/' "${FILE}"

		# even openssl 1.0.1.x has library file names ending in .so.1.0.0
		sed -i -e 's/libssl.so.1.0.1/libssl.so.1.0.0/' \
			-e 's/libcrypto.so.1.0.1/libcrypto.so.1.0.0/' \
			"${FILE}"
	done

	# now that we fixed dynamic linking let's remove binary libs
	# we create symlinks in package() function
	rm "${srcdir}"/extract/vmware-horizon-pcoip/pcoip/lib/vmware/lib{crypto,ssl}.so.1.0.1
}

package_vmware-horizon-client() {
	conflicts=('vmware-view-open-client' 'vmware-view-open-client-beta' 'vmware-view-client')
	depends=('gnome-icon-theme')
	optdepends=('freerdp: RDP remote desktop connections'
		'rdesktop: RDP remote desktop connections'
		'vmware-horizon-pcoip: PCoIP remote desktop connections'
		'vmware-horizon-rtav: Real-Time Audio-Video'
                'vmware-horizon-usb: Redirect USB devices')
	depends_x86_64=('lib32-openssl' 'lib32-libpng12' 'lib32-gtk2' 'lib32-libxml2' 'lib32-libxss')
	depends_i686=('openssl' 'libpng12' 'gtk2' 'libxml2' 'libxss')
	install=vmware-horizon-client.install

	cd "${srcdir}/extract/vmware-horizon-client/"

	mkdir -p "${pkgdir}/usr/"
	cp -a bin/ "${pkgdir}/usr/"
	cp -a lib/ "${pkgdir}/usr/"
	cp -a share/ "${pkgdir}/usr/"

	mkdir -p "${pkgdir}/usr/share/doc/"
	cp -a doc/ "${pkgdir}/usr/share/doc/vmware-horizon-client"
	cp -a debug/ "${pkgdir}/usr/share/doc/vmware-horizon-client/"

	install -D -m0644 "${srcdir}/vmware-horizon-client.desktop" "${pkgdir}/usr/share/applications/vmware-horizon-client.desktop"
}

package_vmware-horizon-pcoip() {
	pkgdesc='VMware Horizon Client connect to VMware Horizon virtual desktop - PCoIP connection'
	depends=('vmware-horizon-client')
	depends_x86_64=('lib32-libxtst')
	optdepends_x86_64=('lib32-alsa-lib: audio support via alsa'
		'lib32-libpulse: audio support via pulse sound server')
	depends_i686=('libxtst')
	optdepends_i686=('alsa-lib: audio support via alsa'
		'libpulse: audio support via pulse sound server')

	cd "${srcdir}/extract/vmware-horizon-pcoip/"

	mkdir -p "${pkgdir}/usr/"
	cp -a pcoip/lib/ "${pkgdir}/usr/lib"
	cp -a pcoip/bin/ "${pkgdir}/usr/bin"

	case ${CARCH} in
		x86_64)
			_libdir="lib32"
			;;
		i686)
			_libdir="lib"
			;;
	esac

	ln -sf ../../${_libdir}/libcrypto.so.1.0.0 "${pkgdir}/usr/lib/vmware/libcrypto.so.1.0.0"
	ln -sf ../../${_libdir}/libssl.so.1.0.0 "${pkgdir}/usr/lib/vmware/libssl.so.1.0.0"
}

package_vmware-horizon-rtav() {
	pkgdesc='VMware Horizon Client connect to VMware Horizon virtual desktop - real-time audio-video (webcam and audio-in)'
	depends=('vmware-horizon-client')
	cd "${srcdir}/extract/vmware-horizon-rtav/"

	mkdir -p "${pkgdir}/usr/"
	cp -a lib/ "${pkgdir}/usr/lib"
}

package_vmware-horizon-usb() {
	pkgdesc='VMware Horizon Client connect to VMware Horizon virtual desktop - USB redirection'
	depends=('vmware-horizon-client')
	install=vmware-horizon-usb.install

	cd "${srcdir}/extract/vmware-horizon-usb/"

	mkdir -p "${pkgdir}/usr/lib/vmware/view/"
	cp -a bin/ "${pkgdir}/usr/lib/vmware/view/usb"

	install -D -m0755 "${srcdir}/vmware-horizon-usb" "${pkgdir}/usr/lib/systemd/scripts/vmware-horizon-usb"
	install -D -m0644 "${srcdir}/vmware-horizon-usb.service" "${pkgdir}/usr/lib/systemd/system/vmware-horizon-usb.service"
	install -D -m0644 "${srcdir}/vmware-horizon-usb-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/vmware-horizon-usb.conf"

	case ${CARCH} in
		x86_64)
			_libdir="lib32"
			;;
		i686)
			_libdir="lib"
			;;
	esac
}

